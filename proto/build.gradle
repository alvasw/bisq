plugins {
    id 'bisq.java-conventions'
    id 'idea'
    id 'com.google.protobuf' version "0.9.3"
}

dependencies {
    implementation enforcedPlatform(project(':platform'))
    annotationProcessor libs.lombok
    compileOnly libs.javax.annotation
    compileOnly libs.lombok
    implementation libs.logback.classic
    implementation libs.logback.core
    implementation libs.google.guava
    implementation libs.protobuf.java
    implementation libs.slf4j.api
    implementation(libs.grpc.protobuf) {
        exclude(module: 'animal-sniffer-annotations')
        exclude(module: 'guava')
    }
    implementation(libs.grpc.stub) {
        exclude(module: 'animal-sniffer-annotations')
        exclude(module: 'guava')
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.19.1"
    }

    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:1.42.1"
        }
    }

    generateProtoTasks {
        ofSourceSet('main').configureEach {
            plugins {
                // Apply the "grpc" plugin whose spec is defined above, without
                // options.  Note the braces cannot be omitted, otherwise the
                // plugin will not be added. This is because of the implicit way
                // NamedDomainObjectContainer binds the methods.
                grpc { }
            }
        }
    }
}
